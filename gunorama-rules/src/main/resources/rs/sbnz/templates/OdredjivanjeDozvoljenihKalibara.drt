template header
zeljenaKonkretnaNamjena

package rs.sbnz.templates;

import rs.sbnz.gunorama.model.enums.*;
import rs.sbnz.gunorama.model.*;
import rs.sbnz.gunorama.model.facts.*;
import rs.sbnz.gunorama.model.faze.*;
import java.util.*;

template "odredjivanje-dozvoljenih-kalibara"

rule "Odredjivanje kalibara uspjesno_@{row.rowNumber}"
  agenda-group "Odredjivanje dozvoljenih kalibara"
  no-loop true
  salience 1
  when 
    $z: Zahtjev($id: id)
    $konkretnaNamjenaFact: KonkretnaNamjenaFact(zahtjevId == $id, konkretnaNamjena == KonkretnaNamjena.@{zeljenaKonkretnaNamjena})
    $kalibri: Set() from accumulate (
      $kalibar: Kalibar( KonkretnaNamjena.@{zeljenaKonkretnaNamjena} memberOf namjene);
      collectSet($kalibar)
    )
    then
      System.out.println("DOZVOLJENI KALIBRI ZA " + KonkretnaNamjena.@{zeljenaKonkretnaNamjena} + " : " + $kalibri);
      insert(new DozvoljeniKalibriFact($id, $kalibri));
end

rule "Odredjivanje kalibara neuspjesno_@{row.rowNumber}"
  agenda-group "Odredjivanje dozvoljenih kalibara"
  no-loop true
  salience -1
  when 
    $z: Zahtjev($id: id)
    $konkretnaNamjenaFact: KonkretnaNamjenaFact(zahtjevId == $id, konkretnaNamjena == KonkretnaNamjena.@{zeljenaKonkretnaNamjena})
    not (DozvoljeniKalibriFact(zahtjevId == $id))
    then
      System.out.println("DOZVOLJENI KALIBRI ZA " + KonkretnaNamjena.@{zeljenaKonkretnaNamjena} + " JE PRAZNA LISTA.");
      insert(new DozvoljeniKalibriFact($id, new HashSet<Kalibar>()));
end

end template